; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "SteamRoller"
#define MyAppVersion "0.1.0.1"
#define MyAppPublisher "Lightly Carnaged"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{AE064E08-F15E-4FD3-8483-53BE3A1B80CE}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={commonpf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=SteamRoller_Setup_{#MyAppVersion}
OutputDir=.\
SetupIconFile=.\Icons\steamroller_offline.ico
Compression=lzma
SolidCompression=yes
SetupLogging=yes
[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "SteamMode.ps1"; DestDir: "{app}"; Flags: ignoreversion
Source: "Readme.txt"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "Icons\steamroller_offline.ico"; DestDir: "{app}"
Source: "Icons\steamroller_online.ico"; DestDir: "{app}"

[Icons]
Name: "{group}\Offline"; Filename: "powershell.exe"; Flags: runminimized; IconFilename: "{app}\steamroller_offline.ico"; IconIndex: 0; Parameters: "-file ""{app}\steammode.ps1"" ""OFFLINE"""; Comment: "Run steam in offline mode"
Name: "{group}\Online"; Filename: "powershell.exe"; Flags: runminimized; IconFilename: "{app}\steamroller_online.ico"; IconIndex: 0; Parameters: "-file ""{app}\steammode.ps1"""; Comment: "Run steam in online mode"
Name: "{autodesktop}\Online"; Filename: "powershell.exe"; Flags: runminimized; IconFilename: "{app}\steamroller_online.ico"; IconIndex: 0; Parameters: "-file ""{app}\steammode.ps1"""; Comment: "Run steam in online mode"
Name: "{autodesktop}\Offline"; Filename: "powershell.exe"; Flags: runminimized; IconFilename: "{app}\steamroller_offline.ico"; IconIndex: 0; Parameters: "-file ""{app}\steammode.ps1"" ""OFFLINE"""; Comment: "Run steam in offline mode"

[InstallDelete]
Type: files; Name: "{userdesktop}\Steam.lnk"
Type: files; Name: "{commondesktop}\Steam.lnk"

[Registry]
Root: "HKLM"; Subkey: "SOFTWARE\Policies\Microsoft\Windows\PowerShell"; ValueType: dword; ValueName: "EnableScripts"; ValueData: "1"; Flags: createvalueifdoesntexist
Root: "HKLM"; Subkey: "SOFTWARE\Policies\Microsoft\Windows\PowerShell"; ValueType: string; ValueName: "ExecutionPolicy"; ValueData: "Unrestricted"; Flags: createvalueifdoesntexist

[Code]
const
  LF = #10;

var SteamPath: String;
    CancelWithoutPrompt: boolean;

procedure CancelButtonClick(CurPageID: Integer; var Cancel, Confirm: Boolean);
begin
  Confirm := not CancelWithoutPrompt;
end;

function PrepareToInstall(var NeedsRestart: Boolean): String;
begin
  if not RegQueryStringValue(HKEY_CURRENT_USER, 'Software\Valve\Steam', 'SteamPath', SteamPath)
  then begin
    CancelWithoutPrompt := true;
    Result := 'Unable to locate Steam.  Make sure Steam is installed and has been run at least once.';
  end
  else Result := '';
end;


procedure CurStepChanged(CurStep: TSetupStep);
var ReadmeContents: TStringList; 
    Contents: String;
begin
  if CurStep = ssPostInstall then
  begin
    // Update path in readme.txt
    ReadmeContents := TStringList.Create;
    ReadmeContents.LoadFromFile(ExpandConstant('{app}') + '\readme.txt');
    Contents := ReadmeContents.text;
    StringChangeEx(Contents, '<apppath>', ExpandConstant('{app}'), FALSE);
    ReadmeContents.Text := Contents;
    ReadmeContents.SaveToFile(ExpandConstant('{app}') + '\readme.txt');
  end;
end;


